plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // H2 Database
    runtimeOnly 'com.h2database:h2'

    // Markdown Renderer
    implementation 'com.vladsch.flexmark:flexmark-all:0.64.8'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// 前端构建任务
task buildFrontend(type: Exec) {
    workingDir file('frontend')
    inputs.dir file('frontend/src')
    outputs.dir file('frontend/dist')

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'npm', 'run', 'build'
    } else {
        commandLine 'sh', '-c', 'npm install && npm run build'
    }
}

// 复制前端构建产物到 static 目录
task copyFrontend(type: Copy) {
    dependsOn buildFrontend
    from file('frontend/dist')
    into file('src/main/resources/static')
}

// 确保 processResources 在 copyFrontend 之后执行
processResources.mustRunAfter copyFrontend

// 确保 bootRun 前先构建前端
bootRun.dependsOn copyFrontend
// 确保 bootJar 前先构建前端
bootJar.dependsOn copyFrontend

tasks.named('test') {
    useJUnitPlatform()
}
